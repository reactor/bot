buildscript {
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
		maven { url 'https://repo.spring.io/plugins-release' }
	}
}

plugins {
	alias(libs.plugins.kotlin)
	alias(libs.plugins.springKotlin) //equivalent to allOpen
	alias(libs.plugins.springBoot)
	alias(libs.plugins.spotless)
	alias(libs.plugins.gradleGitProperties)
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'

group = 'io.projectreactor'
version = '0.0.5-SNAPSHOT'
sourceCompatibility = 1.8

ext {
	isCiServer = System.getenv().containsKey("CI")
}

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	implementation('org.springframework.boot:spring-boot-starter-webflux')
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation(libs.kotlin.stdlib)
	implementation(libs.kotlin.reflect)
	implementation('io.projectreactor.kotlin:reactor-kotlin-extensions')
	implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('io.projectreactor:reactor-test')
	annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
}

springBoot {
	buildInfo()
}

test {
	useJUnitPlatform()
}

spotless {
	if (project.hasProperty("spotlessFrom")) {
		if (project.spotlessFrom == "ALL") {
			println "[Spotless] Ratchet deactivated"
		}
		else {
			println "[Spotless] Ratchet from $project.spotlessFrom"
			ratchetFrom project.spotlessFrom
		}
	}
	else if (isCiServer) {
		println "[Spotless] CI detected without explicit branch, not enforcing check"
		enforceCheck false
	}
	else {
		String spotlessBranch = "origin/main"
		println "[Spotless] Local run detected, ratchet from $spotlessBranch"
		ratchetFrom spotlessBranch
	}
	kotlin {
		licenseHeaderFile('codequality/spotless/licenseSlashstarStyle.txt')
	}
	java {
		licenseHeaderFile('codequality/spotless/licenseSlashstarStyle.txt')
	}
}


compileJava.dependsOn(processResources)