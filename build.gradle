buildscript {
	ext {
		kotlinVersion = '1.3.41'
		springBootVersion = '2.2.4.RELEASE'
		propDepsVersion = '0.0.9.RELEASE'
		gitPropertiesVersion = '2.0.0'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url 'https://repo.spring.io/plugins-release' }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath("io.spring.gradle:propdeps-plugin:${propDepsVersion}")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gitPropertiesVersion}")
	}
}

plugins {
	id "com.diffplug.spotless" version "5.14.0"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'
apply plugin: 'com.gorylenko.gradle-git-properties'

group = 'io.projectreactor'
version = '0.0.5-SNAPSHOT'
sourceCompatibility = 1.8

ext {
	isCiServer = System.getenv().containsKey("CI")
}

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-webflux')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
	compile('io.projectreactor.kotlin:reactor-kotlin-extensions')
  	compile("com.fasterxml.jackson.module:jackson-module-kotlin")
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('io.projectreactor:reactor-test')
	optional "org.springframework.boot:spring-boot-configuration-processor"
}

springBoot {
  buildInfo()
}

spotless {
	if (project.hasProperty("spotlessFrom")) {
		if (project.spotlessFrom == "ALL") {
			println "[Spotless] Ratchet deactivated"
		}
		else {
			println "[Spotless] Ratchet from $project.spotlessFrom"
			ratchetFrom project.spotlessFrom
		}
	}
	else if (isCiServer) {
		println "[Spotless] CI detected without explicit branch, not enforcing check"
		enforceCheck false
	}
	else {
		String spotlessBranch = "origin/main"
		println "[Spotless] Local run detected, ratchet from $spotlessBranch"
		ratchetFrom spotlessBranch
	}
	kotlin {
		licenseHeaderFile('codequality/spotless/licenseSlashstarStyle.txt')
	}
	java {
		licenseHeaderFile('codequality/spotless/licenseSlashstarStyle.txt')
	}
}


compileJava.dependsOn(processResources)